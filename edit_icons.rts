'------------------------------------------------------------------------------
' Edit Icons: Replacing Icon Resource with Icon from .ICO File
'------------------------------------------------------------------------------
'
' This sample VBScript code provides a real-world example that demonstrates
' many of the features available in Resource Tuner Console. 
'
' This code shows how to modify icons in executables:
'   - Replace the main icon with the new icons from an .ICO file (including  
'     the 256x256 PNG-compressed Vista icon); 
'   - Add one more icon to the newly created Icon Group; 
'   - Output the changes to the Resource Tree to a log file;
'   - Restore the original timestamps on the file after saving (for demo purpose).
'
' To give you an idea of how this all works, we made this sample script and
' test application. When you installed Resorce Tuner Console on your computer,
' the setup program created the ".\Demo Scripts" folder under the RTC folder, with 
' the test application "DemoApp1.exe" in the ".\Demo Scripts\Edit Icons\Src" folder. 
'
' This script is ready to run. Select EDIT_ICONS.BAT file to execute the script.
'
' The script will add or replace icons in DemoApp1.exe with icons from the .ico
' files from the ".\Src" folder. 
'
' The resulting file will be created in the directory named ".\Release"
' Check the log file "log.txt" to see the Resource Tree changed. 
'
' Demonstrates the following:
'
' - PEFileProxy.OpenFile
' - PEFileProxy.Terminated
' - PEFileProxy.HasResources
' - PEFileProxy.CreateBackUp
' - PEFileProxy.SaveAsNewImage
' - PEFileProxy.PostDebugString
' - PEFileProxy.UpdateCheckSum
' - ResourcesProxy.ChangeIcon
' - ResourcesProxy.ResourceTreeToLog
' - ScriptUnit.SetFileTime
'
'---------------------------------------------------------------------------------------
Sub Main
  PEFileProxy.PostDebugString "Updating the checksum in the PE file header is enabled." 
  PEFileProxy.UpdateCheckSum = True 

  PEFileProxy.PostDebugString "The creation of a backup copy is disabled." 
  PEFileProxy.CreateBackUp = False 

  '-------- Getting the original file timestamps (for demo purpose) --------------------
  'To restore the original timestamps on the file after saving,
  'we need to get the timestamps before performing any file operations.

  SRCFILE = ".\spc1500_demo2.exe"
  PEFileProxy.PostDebugString ""
  PEFileProxy.PostDebugString "Getting CreationTime, LastAccessTime and LastWriteTime for file '" & SRCFILE & "'..."
  set fso = CreateObject("Scripting.FileSystemObject")
  set f = fso.GetFile(SRCFILE)
  DCR = f.DateCreated
  DLM = f.DateLastModified
  DLA = f.DateLastAccessed
  PEFileProxy.PostDebugString "Created      : " & DCR
  PEFileProxy.PostDebugString "Last Modified: " & DLM
  PEFileProxy.PostDebugString "Last Accessed: " & DLA
  PEFileProxy.PostDebugString ""
  '--------------------------------------------------------------------------------------

  'Open file
  PEFileProxy.PostDebugString "Opening the file..."
  PEFileProxy.OpenFile SRCFILE
  if (PEFileProxy.Terminated) then
    'Issue a warning in case of error
    PEFileProxy.PostDebugString "Fatal error when opening this file."
  else
    PEFileProxy.PostDebugString "File opened OK."
    if (not PEFileProxy.HasResources) then
      PEFileProxy.PostDebugString "The file contains no resources."
    else
      PEFileProxy.PostDebugString "The file contains resources."

      'This specifies the default language      
      LangID = 0

      PEFileProxy.PostDebugString "Changing/adding the main application icon..."
      ResourcesProxy.ChangeIcon "", LangID, CREATE_IF_NOT_EXIST, REPLACE_IF_ITEM_EXISTS_ELSE_ADD, ".\src\mame\osd\windows\mame\mame.ico"

      'PEFileProxy.PostDebugString "Adding one more Icon Group with one more icon..."
      'ResourcesProxy.ChangeIcon "New Icon Group", LangID, CREATE_IF_NOT_EXIST, REPLACE_IF_ITEM_EXISTS_ELSE_ADD, ".\src\icon_3_32x32_8bit.ico"

      'Build and output the Resource Tree to log to show the changes made to the resources
      PEFileProxy.PostDebugString ""
      PEFileProxy.PostDebugString "Resource Tree built by RTC:"
      ResourcesProxy.ResourceTreeToLog
      PEFileProxy.PostDebugString ""

      'Save file
      PEFileProxy.PostDebugString "Saving file..."
      DESTFILE = ".\spc1500_demo2.exe"
      PEFileProxy.SaveAsNewImage DESTFILE

  '--- Restoring the original file timestamps (CreationTime, LastAccess and LastWrite) --
      PEFileProxy.PostDebugString ""
      PEFileProxy.PostDebugString "Setting CreationTime, LastAccessTime and LastWriteTime for file '" & DESTFILE & "'..."
      if not ScriptUnit.SetFileTime(DESTFILE, DCR, DLA, DLM) then
       PEFileProxy.PostDebugString "Setting CreationTime, LastAccessTime and LastWriteTime failed for file '" & DESTFILE & "'."
      else
       PEFileProxy.PostDebugString "CreationTime, LastAccessTime and LastWriteTime for file '" & DESTFILE & "' set OK."
      end if
      PEFileProxy.PostDebugString ""
  '--------------------------------------------------------------------------------------

    end if
    PEFileProxy.PostDebugString "Closing this file..."
    PEFileProxy.CloseFile
  end if
end Sub


